plugins {
    id "org.springframework.boot" version "2.5.6"
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id "java-library"
    id "maven-publish"
}

group = "ru.softdarom.security"
version = "1.0.0"
sourceCompatibility = "11"

repositories {
    mavenCentral()
}

configurations {
    annotationProcessor {
        extendsFrom compileOnly
    }

    testAnnotationProcessor {
        extendsFrom testCompileOnly
    }
}

ext {
    set("gpr.user", project.findProperty("gpr.user") ?: System.getenv("GPR_USERNAME"))
    set("gpr.token", project.findProperty("gpr.token") ?: System.getenv("GPR_TOKEN"))
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2020.0.1"
    }
}

dependencies {
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.security.oauth:spring-security-oauth2:2.5.1.RELEASE"
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"
    api "org.springframework.boot:spring-boot-starter-webflux"

    api 'com.google.guava:guava:29.0-jre'

    compileOnly "org.projectlombok:lombok"
    compileOnly "org.springframework.boot:spring-boot-configuration-processor"
}

jar {
    manifest {
        attributes(
                "Implementation-Title": project.name,
                "Implementation-Version": project.version
        )
    }
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

publishing {
    publications {
        nexus(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
        }
    }

    repositories {
        maven {
            name = "GitHubPackage"
            url = uri("https://maven.pkg.github.com/softdarom/${project.name}")
            credentials {
                username = project.property("gpr.user")
                password = project.property("gpr.token")
            }
        }
    }
}